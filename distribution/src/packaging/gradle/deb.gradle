/*
 *
 * Copyright (c) 2015 Caricah <info@caricah.com>.
 *
 * Caricah licenses this file to you under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License. You may obtain a copy
 *  of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 *  Unless required by applicable law or agreed to in writing, software distributed under
 *  the License is distributed on an "AS IS" BASIS, WITHOUT WARRANTIES OR CONDITIONS
 *  OF ANY  KIND, either express or implied.  See the License for the specific language
 *  governing permissions and limitations under the License.
 *
 *
 *
 *
 */

import org.apache.tools.ant.filters.*

def debian_packaging = new Properties()

task processDebScripts(type: Copy) {

    debian_packaging.setProperty("project.build.finalName","$project.name-$project.version")
    debian_packaging.setProperty("project.name",project.name)
    debian_packaging.setProperty("project.version",project.version)
    debian_packaging.load(new FileInputStream("distribution/src/packaging/packaging.properties"))
    debian_packaging.load(new FileInputStream("distribution/deb/packaging.properties"))

    debian_packaging.put("build.temporary.scripts.assembly.dir", "$buildDir/tmp/script")

    from "distribution/src/packaging/scripts/"
    into debian_packaging."build.temporary.scripts.assembly.dir"
    fileMode = 0755
    filter(ReplaceTokens, tokens: debian_packaging)

}


buildDeb {

    //tasks.processDebScripts.execute()


    packageName 'iotracah'
    os LINUX
    maintainer 'Bwire J. Peter <info@caricah.com>'
    priority 'optional'
    url 'http://io.tracah.com'
    requires('adduser')
    permissionGroup 'root'
    user 'root'
    license 'Apache License'
    packageGroup 'net'
    release getRevision()

    packageDescription debian_packaging."packaging.project.description"
    summary debian_packaging."packaging.project.summary"

    into (debian_packaging."packaging.iotracah.home.dir") {

        from(jar.outputs.files) {
            into 'lib'
            fileMode = 0644
        }

        from(configurations.runtime) {
            into 'lib'
            fileMode = 0644
        }

        from('./'){
            include 'NOTICE.txt'
            fileMode = 0644
            fileType DOC
        }

        from('./'){
            include 'README.md'
            fileMode = 0644
            fileType DOC
        }


        from('distribution/src/resources/bin') {

            into 'bin'
            fileMode 0755
            exclude '*.bat'
            exclude '*.exe'

            filter(ReplaceTokens, tokens: debian_packaging)
        }

        into(debian_packaging."packaging.iotracah.conf.dir"){
            from('distribution/src/resources/config') {
                fileType CONFIG | NOREPLACE
                filter(ReplaceTokens, tokens: debian_packaging)
                fileMode = 0644
            }
        }

        //Add systemd service.
        into(debian_packaging."packaging.iotracah.systemd.dir"){
            from('distribution/src/packaging/systemd/') {
                include('iotracah.service')
                filter(ReplaceTokens, tokens: debian_packaging)
                fileMode = 0644
            }
        }

        //Add systemd/sysctl.d configuration file.
        into(debian_packaging."packaging.iotracah.systemd.sysctl.dir"){
            from('distribution/src/packaging/systemd/sysctl') {
                include('iotracah.conf')
                filter(ReplaceTokens, tokens: debian_packaging)
                fileMode = 0644
            }
        }

        //Adds systemd/tmpfiles.d configuration file.
        into(debian_packaging."packaging.iotracah.tmpfilesd.dir"){
            from('distribution/src/packaging/systemd/') {
                include('tmp_iotracah.conf')
                rename('tmp_iotracah.conf', 'iotracah.conf')
                filter(ReplaceTokens, tokens: debian_packaging)
                fileMode = 0644
            }
        }


    }

    preInstall file(debian_packaging."build.temporary.scripts.assembly.dir"+"/preinst")
    postInstall file(debian_packaging."build.temporary.scripts.assembly.dir"+"/postinst")
    preUninstall file(debian_packaging."build.temporary.scripts.assembly.dir"+"/prerm")
    postUninstall file(debian_packaging."build.temporary.scripts.assembly.dir"+"/postrm")

    configurationFile(
            debian_packaging."packaging.iotracah.conf.dir"+'/iotracah.properties\n' +
                    debian_packaging."packaging.iotracah.conf.dir"+'/log4j.properties\n' +
                    debian_packaging."packaging.iotracah.conf.dir"+'/security.ini\n' +
                    debian_packaging."packaging.iotracah.int.d.dir"+'/iotracah')


    from('distribution/deb/init.d/'){
        into debian_packaging."packaging.iotracah.int.d.dir"
        include 'iotracah'
        fileMode = 0755
        filter(ReplaceTokens, tokens: debian_packaging)
    }

    from('distribution/deb/lintian/') {
        include 'iotracah'
        fileMode = 0644
        into debian_packaging.'packaging.debian.lintian.overrides.dir'
    }

    from('distribution/deb/scripts/') {
        include 'conffiles'
        fileMode = 0644
        into debian_packaging.'packaging.debian.lintian.overrides.dir'
    }

    from('distribution/') {
        include 'copyright'
        fileMode = 0644
        into debian_packaging.'packaging.debian.doc.dir'
    }

}.dependsOn processDebScripts